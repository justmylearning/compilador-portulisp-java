<programa> ::= <comando> <programa> | <funcao> <programa> |
<funcao> ::= '(' <funcao-interna> ')'
<funcao-interna> ::= 'fun' id <params> ':' <tipo> <comandos>
<params> ::= <param> <params> |
<param> ::= '(' <tipo> id ')'
<tipo> ::= 'int' | 'real' | 'texto' | 'logico' | 'nada'
<comandos> ::= <comando> <comandos> |
<comando> ::= '(' <comando-interno> ')'
<comando-interno> ::= <decl> | <atrib> | <invoca> | <se> | <leitura> | <enquanto> | <para> | <retorno> | <mostrar>
<decl> ::= <tipo> <ids>
<ids> ::= id <ids2>
<ids2> ::= id <ids2> |
<atrib> ::= '=' id <expr>
<expr> ::= <operan> | '(' <op2> <expr> <expr> ')' | '(' <op1> id ')'
<op2> ::= '&&' | '||' | '>' | '>=' | '<' | '<=' | '==' | '!=' | '.' | '+' | '-' | '*' | '/'
<op1> ::= '++' | '--'
<invoca> ::= id <args>
<args> ::= <expr> <args> |
<operan> ::= id | cli | clr | cll | cls | '(' <invoca> ')'
<se> ::= 'se' <expr> '(' <comandos> ')' <senao>
<senao> ::= '(' <comandos> ')' |
<leitura> ::= 'le' id
<mostrar> ::= 'mostra '<expr>
<enquanto> ::= 'enquanto' <expr> <comandos>
<para> ::= 'para' <atrib> <expr> <atrib> <comandos>
<retorno> ::= 'ret' <expr>

//FIRST
<programa> =>
    <comando> => '('
    <funcao> => '('
    &

